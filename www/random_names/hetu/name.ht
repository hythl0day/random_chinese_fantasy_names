import 'constants.ht'
import '../data/shared/common.json' as commonNames
import '../data/shared/strange.json' as strangeNames
import '../data/shared/color.json' as colorPrefix
import '../data/shared/spirit.json' as spiritPrefix
import '../data/name/family.json' as family
import '../data/name/female.json' as female
import '../data/name/male.json' as male
import '../data/name/middle.json' as middle
import '../data/dao/dao.json' as dao
import '../data/dao/title_male.json' as daoTitleMale
import '../data/dao/title_female.json' as daoTitleFemale
import '../data/skill/skill.json' as skill
import '../data/skill/prefix.json' as skillPrefix
import '../data/skill/numfix.json' as skillNumfix
import '../data/book/prefix.json' as bookPrefix
import '../data/book/book.json' as book
import '../data/book/postfix.json' as bookPostfix
import '../data/talisman/talisman.json' as talisman
import '../data/talisman/material.json' as talismanMaterial
import '../data/talisman/postfix.json' as talismanPostfix
import '../data/organization/clan.json' as clan
import '../data/organization/nation.json' as nation
import '../data/place/place.json' as place
import '../data/place/prefix.json' as placePrefix
import '../data/place/postfix.json' as placePostfix
import '../data/place/location.json' as location
import '../data/place/continent.json' as continent
import '../data/material/material.json' as material
import '../data/material/postfix.json' as materialPostfix
import '../data/creature/creature.json' as creature
import '../data/creature/prefix.json' as creaturePrefix
import '../data/creature/strange.json' as strangeCreature
import '../data/alchemy/alchemy.json' as alchemy

export {
  kSex,
  kRarity,
  kCreature,
} from 'constants.ht'

export {
  getName,
  getDao,
  getSkill,
  getBook,
  getCreature,
  getMaterial,
  getAlchemy,
  getTalisman,
  getClan,
  getNation,
  getLocation,
  getContinent,
}

fun getRarity(max) {
  var rarity
  final value = Math.random() * (max || 1.0)
  if (value < kRarity.exotic.probability) {
    rarity = 'exotic'
  } else if (value < kRarity.mythic.probability) {
    rarity = 'mythic'
  } else if (value < kRarity.legendary.probability) {
    rarity = 'legendary'
  } else if (value < kRarity.epic.probability) {
    rarity = 'epic'
  } else if (value < kRarity.rare.probability) {
    rarity = 'rare'
  } else if (value < kRarity.uncommon.probability) {
    rarity = 'uncommon'
  } else {
    rarity = 'common'
  }
  return { rarity, value }
}

const _kParenthesisLeft = '（'
const _kParenthesisRight = '）'

final common = [
  ...commonNames.dao,
  ...commonNames.element,
  ...commonNames.creature,
  ...commonNames.thing,
  ...commonNames.color,
  ...commonNames.place,
  ...commonNames.adj,
  ...commonNames.number,
  ...commonNames.gesture,
  ...commonNames.action,
];

fun getName(number, {isFemale, style, familyName, middleCharacter}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var theFamilyName
    if (!familyName) {
      final familyIndex = Math.randomInt(family.length)
      theFamilyName = family[familyIndex]
    } else {
      theFamilyName = familyName
    }
    final f = isFemale ?? Math.randomInt(10) % 2 == 0
    final namesOfASex = f ? female : male
    final r = Math.random()
    var s
    if (style == null || style == 'random') {
      s = r < 0.33333333 ? 'single' : r < 0.66666666 ? 'double' : 'combine'
    } else {
      s = style
    }
    var name = ''
    if (s == 'single') {
      if (middleCharacter) {
        name = middleCharacter
      } else {
        final nameIndex = Math.randomInt(namesOfASex.length)
        name = namesOfASex[nameIndex]
      }
    } else if (s == 'double') {
      final theMiddleCharacter
      if (middleCharacter) {
        theMiddleCharacter = middleCharacter
      } else {
        final nameIndex = Math.randomInt(namesOfASex.length)
        theMiddleCharacter = namesOfASex[nameIndex]
      }
      final nameIndex = Math.randomInt(namesOfASex.length)
      final theLastCharacter = namesOfASex[nameIndex]
      name = theMiddleCharacter + theLastCharacter
    } else {
      final theMiddleCharacter
      if (middleCharacter) {
        theMiddleCharacter = middleCharacter
      } else {
        final nameIndex = Math.randomInt(middle.length)
        theMiddleCharacter = middle[nameIndex]
      }
      final nameIndex = Math.randomInt(namesOfASex.length)
      final theLastCharacter = namesOfASex[nameIndex]
      name = theMiddleCharacter + theLastCharacter
    }
    names.add(theFamilyName + name)
  }
  return names
}

fun getDao(number, {isFemale, title, firstCharacter}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var theFirstCharacter
    if (firstCharacter) {
      theFirstCharacter = firstCharacter
    } else {
      final nameIndex1 = Math.randomInt(dao.length)
      theFirstCharacter = dao[nameIndex1]
    }
    final nameIndex2 = Math.randomInt(dao.length)
    final name = theFirstCharacter + dao[nameIndex2]
    final titleGroup =
      isFemale ?? Math.randomInt(10) % 2 == 0
        ? daoTitleFemale
        : daoTitleMale
    var t = ''
    var rarity = 'common'
    if (!title) {
      rarity = getRarity().rarity
      if (rarity == 'exotic') {
        t =
          titleGroup.exotic[
            Math.randomInt(titleGroup.exotic.length)
          ]
      } else if (rarity == 'mythic') {
        t =
          titleGroup.mythic[
            Math.randomInt(titleGroup.mythic.length)
          ]
      } else if (rarity == 'legendary') {
        t =
          titleGroup.legendary[
            Math.randomInt(titleGroup.legendary.length)
          ]
      } else if (rarity == 'epic') {
        t = titleGroup.epic[Math.randomInt(titleGroup.epic.length)]
      } else if (rarity == 'rare') {
        t = titleGroup.rare[Math.randomInt(titleGroup.rare.length)]
      } else if (rarity == 'uncommon') {
        t =
          titleGroup.uncommon[
            Math.randomInt(titleGroup.uncommon.length)
          ]
      }
    }
    names.add({ name: name + t, rarity })
  }
  return names
}

const _kNumberBeginSupplement = '路'
const _kNumberEndSupplement = '式'

fun _getSkillName({length, kind, prefix, numfix}) {
  var l = length || 1
  var rarity = 'common'
  if (!length) {
    final r = getRarity()
    if (r.value < kRarity.rare.probability) {
      l = 3
    } else if (r.value < kRarity.uncommon.probability) {
      l = 2
    }
    rarity = r.rarity
  }
  var name = ''
  var pre = prefix ?? ''
  var n = numfix ?? ''
  final k = kind ?? skill[Math.randomInt(skill.length)]
  for (var i = 0; i < l; ++i) {
    name += common[Math.randomInt(common.length)]
  }
  if (!prefix && Math.random() < kRarity.epic.probability) {
    pre = skillPrefix[Math.randomInt(skillPrefix.length)]
  }
  if (!numfix && Math.random() < kRarity.epic.probability) {
    n = skillNumfix[Math.randomInt(skillNumfix.length)]
  }
  if (Math.random() < 0.5) {
    name = (n != '' ? n + _kNumberBeginSupplement : '') + pre + name + k
  } else {
    if (k.length > 1) {
      name = pre + name + k + (n != '' ? n + _kNumberEndSupplement : '')
    } else {
      name = pre + name + n + k
    }
  }
  return { name, rarity }
}

fun getSkill(number, {length, kind, prefix, numfix}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    final name = _getSkillName(length: length, kind: kind, prefix: prefix, numfix: numfix)
    names.add(name)
  }
  return names
}

fun getBook(number, {length, prefix, kind, postfix}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    final skillname = _getSkillName(length)
    var rarity = skillname.rarity
    var pre = prefix ?? ''
    var k = kind ?? ''
    var post = postfix ?? ''
    if (!prefix) {
      if (skillname.rarity == 'exotic') {
        pre =
          bookPrefix.exotic[
            Math.randomInt(bookPrefix.exotic.length)
          ]
      } else if (skillname.rarity == 'mythic') {
        pre =
          bookPrefix.mythic[
            Math.randomInt(bookPrefix.mythic.length)
          ]
      } else if (skillname.rarity == 'legendary') {
        pre =
          bookPrefix.legendary[
            Math.randomInt(bookPrefix.legendary.length)
          ]
      } else if (skillname.rarity == 'epic') {
        pre =
          bookPrefix.epic[Math.randomInt(bookPrefix.epic.length)]
      }
    }
    if (pre && !kind) {
      k = book[Math.randomInt(book.length)]
    }
    if (!prefix && !kind) {
      if (!postfix) {
        final r1 = Math.random()
        final r2 = Math.random()
        if (r1 < kRarity.rare.probability && r2 < kRarity.rare.probability) {
          rarity = 'handmade'
          post =
            _kParenthesisLeft +
            bookPostfix.rare[
              Math.randomInt(bookPostfix.rare.length)
            ] +
            _kParenthesisRight
        } else if (r1 < kRarity.uncommon.probability && r2 < kRarity.uncommon.probability) {
          rarity = 'broken'
          post =
            _kParenthesisLeft +
            bookPostfix.uncommon[
              Math.randomInt(bookPostfix.uncommon.length)
            ] +
            _kParenthesisRight
        }
      }
    } else {
      rarity = 'rare'
    }
    names.add({
      name: '《' + skillname.name + pre + k + post + '》',
      rarity: rarity,
    })
  }
  return names
}

final commonCreatureNames = [
  ...commonNames.dao,
  ...commonNames.element,
  ...commonNames.thing,
  ...commonNames.color,
  ...commonNames.number,
  ...commonNames.action,
]

fun getCreature(number, {category, rarity}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    final pre =
      commonCreatureNames[
        Math.randomInt(commonCreatureNames.length)
      ]
    final c = colorPrefix[Math.randomInt(colorPrefix.length)]
    final s = creaturePrefix[Math.randomInt(creaturePrefix.length)]
    var cat = category
    if (!category) {
      final creatureList = kCreature.keys
      cat =
        creatureList[Math.randomInt(creatureList.length)]
    }
    final k = creature[cat][Math.randomInt(creature[cat].length)]
    final r = rarity ?? getRarity(kRarity.uncommon.probability).rarity
    if (r == 'exotic') {
      name =
        strangeCreature[Math.randomInt(strangeCreature.length)]
    } else if (r == 'mythic') {
      name = pre + c + s + k
    } else if (r == 'legendary') {
      name = pre + s + k
    } else if (r == 'epic') {
      name = pre + c + k
    } else if (r == 'rare') {
      name = pre + k
    } else if (r == 'uncommon') {
      name = c + s + k
    } else if (r == 'common') {
      name = c + k
    }
    names.add({ name, rarity: r, category: cat })
  }
  return names
}

const _kAge1 = '百年'
const _kAge10 = '千年'
const _kAge100 = '万年'

fun getMaterial(number, {kind, rarity, postfix}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    var age = ''
    final pre = common[Math.randomInt(common.length)]
    final c = colorPrefix[Math.randomInt(colorPrefix.length)]
    final s = spiritPrefix[Math.randomInt(spiritPrefix.length)]
    var k = kind
    var post = postfix ?? ''
    final r = rarity ?? getRarity(kRarity.uncommon.probability).rarity
    if (r == 'exotic') {
      final t = [
        ...material.exotic,
        ...material.mythic,
        ...material.legendary,
        ...material.epic,
        ...material.rare,
        ...material.uncommon,
        ...material.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      age = _kAge100
      name = age + pre + c + s + k
    } else if (r == 'mythic') {
      final t = [
        ...material.mythic,
        ...material.legendary,
        ...material.epic,
        ...material.rare,
        ...material.uncommon,
        ...material.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      age = _kAge10
      name = age + pre + c + s + k
    } else if (r == 'legendary') {
      final t = [
        ...material.legendary,
        ...material.epic,
        ...material.rare,
        ...material.uncommon,
        ...material.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      age = _kAge1
      name = age + pre + c + s + k
    } else if (r == 'epic') {
      final t = [
        ...material.epic,
        ...material.rare,
        ...material.uncommon,
        ...material.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      name = pre + c + s + k
    } else if (r == 'rare') {
      final t = [...material.rare, ...material.uncommon, ...material.common]
      k ??= t[Math.randomInt(t.length)]
      name = pre + s + k
    } else if (r == 'uncommon') {
      final t = [...material.uncommon, ...material.common]
      k ??= t[Math.randomInt(t.length)]
      name = c + s + k
    } else if (r == 'common') {
      k ??= material.common[Math.randomInt(material.common.length)]
      name = c + k
    }
    if (!postfix) {
      final r1 = Math.random()
      final r2 = Math.random()
      if (r1 < kRarity.rare.probability && r2 < kRarity.rare.probability) {
        post =
          _kParenthesisLeft +
          materialPostfix.broken[
            Math.randomInt(materialPostfix.broken.length)
          ] +
          _kParenthesisRight
      } else if (r1 < kRarity.uncommon.probability && r2 < kRarity.uncommon.probability) {
        post =
          _kParenthesisLeft +
          materialPostfix.handmade[
            Math.randomInt(materialPostfix.handmade.length)
          ] +
          _kParenthesisRight
      }
    } else {
      post = _kParenthesisLeft + post + _kParenthesisRight
    }
    names.add({ name: name + post, rarity: r })
  }
  return names
}

final commonAlchemyNames = [
  ...commonNames.dao,
  ...commonNames.element,
  ...commonNames.color,
  ...commonNames.number,
  ...commonNames.action,
]

fun getAlchemy(number, {kind}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var rarity = 'common'
    final pre =
      commonAlchemyNames[Math.randomInt(commonAlchemyNames.length)]
    var s = ''
    final r = getRarity()
    if (r.value < kRarity.rare.probability) {
      s = spiritPrefix[Math.randomInt(spiritPrefix.length)]
    }
    rarity = r.rarity
    var k = kind ?? ''
    if (!kind) {
      k = alchemy[Math.randomInt(alchemy.length)]
    }
    names.add({ name: pre + s + k, rarity })
  }
  return names
}

fun getTalisman(number, {kind, rarity}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    final prefix = common[Math.randomInt(common.length)]
    final c = colorPrefix[Math.randomInt(colorPrefix.length)]
    final m =
      talismanMaterial[Math.randomInt(talismanMaterial.length)]
    final s = spiritPrefix[Math.randomInt(spiritPrefix.length)]
    final k = kind
    final r = rarity ?? getRarity(kRarity.uncommon.probability).rarity
    if (r == 'exotic') {
      final t = [
        ...talisman.exotic,
        ...talisman.mythic,
        ...talisman.legendary,
        ...talisman.epic,
        ...talisman.rare,
        ...talisman.uncommon,
        ...talisman.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      name = prefix + s + k
    } else if (r == 'mythic') {
      final t = [
        ...talisman.mythic,
        ...talisman.legendary,
        ...talisman.epic,
        ...talisman.rare,
        ...talisman.uncommon,
        ...talisman.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      name = prefix + s + k
    } else if (r == 'legendary') {
      final t = [
        ...talisman.legendary,
        ...talisman.epic,
        ...talisman.rare,
        ...talisman.uncommon,
        ...talisman.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      name = prefix + c + m + k
    } else if (r == 'epic') {
      final t = [
        ...talisman.epic,
        ...talisman.rare,
        ...talisman.uncommon,
        ...talisman.common,
      ]
      k ??= t[Math.randomInt(t.length)]
      name = prefix + m + k
    } else if (r == 'rare') {
      final t = [...talisman.rare, ...talisman.uncommon, ...talisman.common]
      k ??= t[Math.randomInt(t.length)]
      name = prefix + k
    } else if (r == 'uncommon') {
      final t = [...talisman.uncommon, ...talisman.common]
      k ??= t[Math.randomInt(t.length)]
      name = c + m + k
    } else if (r == 'common') {
      k ??= talisman.common[Math.randomInt(talisman.common.length)]
      name = m + k
    }
    var post = ''
    final r1 = Math.random()
    final r2 = Math.random()
    if (r1 < kRarity.rare.probability && r2 < kRarity.rare.probability) {
      post =
        _kParenthesisLeft +
        talismanPostfix.broken[
          Math.randomInt(talismanPostfix.broken.length)
        ] +
        _kParenthesisRight
    } else if (r1 < kRarity.uncommon.probability && r2 < kRarity.uncommon.probability) {
      post =
        _kParenthesisLeft +
        talismanPostfix.handmade[
          Math.randomInt(talismanPostfix.handmade.length)
        ] +
        _kParenthesisRight
    }
    names.add({ name: name + post, rarity: r })
  }
  return names
}

fun getClan(number, {kind}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    final name = common[Math.randomInt(common.length)]
    final k = kind
    if (!k) {
      k = clan[Math.randomInt(clan.length)]
    }
    names.add(name + k)
  }
  return names
}

const _kContry = '国'

fun getNation(number, {kind}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    var k = kind ?? ''
    var rarity = 'common'
    final r = Math.random()
    if (r < kRarity.rare.probability) {
      name = strangeNames[Math.randomInt(strangeNames.length)]
      rarity = 'rare'
      if (!kind) {
        if (name.length == 1) {
          k = _kContry
        } else {
          k = nation[Math.randomInt(nation.length)]
        }
      }
    } else if (r < kRarity.uncommon.probability) {
      name = common[Math.randomInt(common.length)]
      rarity = 'uncommon'
      if (!kind) {
        if (name.length == 1) {
          k = _kContry
        } else {
          k = nation[Math.randomInt(nation.length)]
        }
      }
    } else {
      var prefix = ''
      if (Math.random() < kRarity.rare.probability) {
        prefix = placePrefix[Math.randomInt(placePrefix.length)]
      }
      name = prefix + place[Math.randomInt(place.length)]
      if (!kind) {
        k = _kContry
      }
    }
    names.add({ name: name + k, rarity })
  }
  return names
}

fun getLocation(number, {kind}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    var k = kind ?? ''
    var rarity = 'common'
    final r = Math.random()
    if (r < kRarity.rare.probability) {
      name = strangeNames[Math.randomInt(strangeNames.length)]
      rarity = 'rare'
    } else if (r < kRarity.uncommon.probability) {
      name = common[Math.randomInt(common.length)]
      rarity = 'uncommon'
    } else {
      final placeIndex = Math.randomInt(place.length)
      var postfix = ''
      if (Math.random() < kRarity.uncommon.probability) {
        final postfixIndex = Math.randomInt(placePostfix.length)
        postfix = placePostfix[postfixIndex]
      }
      name = place[placeIndex] + postfix
    }
    if (!kind) {
      k = location[Math.randomInt(location.length)]
    }
    names.add({ name: name + k, rarity })
  }
  return names
}

const _kContinent = '洲'

fun getContinent(number, {kind}) {
  final names = []
  for (var i = 0; i < number; ++i) {
    var name = ''
    var k = kind ?? ''
    if (!kind) {
      k = continent[Math.randomInt(continent.length)]
    }
    var rarity = 'common'
    final r = Math.random()
    if (r < kRarity.rare.probability) {
      name = strangeNames[Math.randomInt(strangeNames.length)]
      rarity = 'rare'
    } else if (r < kRarity.uncommon.probability) {
      name = common[Math.randomInt(common.length)]
      rarity = 'uncommon'
    } else {
      var prefix = ''
      if (Math.random() < kRarity.rare.probability) {
        prefix = placePrefix[Math.randomInt(placePrefix.length)]
      }
      name = prefix + place[Math.randomInt(place.length)]
      if (name.length == 1) {
        k = _kContinent
      }
    }
    names.add({ name: name + k, rarity })
  }
  return names
}
